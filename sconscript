#!/usr/bin/python

import functools
import os.path
import subprocess

import SCons.Scanner.C

# Import variables.
Import(['env', 'src'])

# Our scanner.
scanner = SCons.Scanner.C.CScanner()

# Current working directory.
cwd = Dir(Dir(GetLaunchDir()).get_path(src))

# Recursively find all of the source files that main depends on.
def get_dep_objects(main):
  def get_dep_sources(sources, out):
    if not sources:
      return out
    out |= sources
    dep_sources = set()
    for source in sources:
      for dep_header in source.get_implicit_deps(env, scanner, (src,)):
        dep_source = File(os.path.splitext(dep_header.get_path(src))[0] + '.cc')
        if dep_source.srcnode().exists() and dep_source not in out:
          dep_sources.add(dep_source)
    return get_dep_sources(dep_sources, out)
  return [env.Object(source) for source in get_dep_sources({main}, set())]

# List of valid extensions to try to build.
exts = [env[suffix] for suffix in ['PROGSUFFIX',
                                   'LIBSUFFIX',
                                   'SHLIBSUFFIX',
                                   'OBJSUFFIX',
                                   'SHOBJSUFFIX',
                                   'TESTSUFFIX']]

for target_path in BUILD_TARGETS:
  target = cwd.File(target_path)
  root, ext = os.path.splitext(target_path)  # root and ext of target_path.
  if ext not in exts:
    raise ValueError(
        'Invalid extension: "{ext}" (choose from {exts})'.format(
          ext=ext,
          exts=', '.join(['"{ext}"'.format(ext=ext) for ext in exts])))
  main = cwd.File((target_path if ext == env['TESTSUFFIX'] else root) + '.cc')
  objects = get_dep_objects(main)
  # Choose the correct builder based on the target_path's extension.
  {env['PROGSUFFIX'] : env.Program,
   env['LIBSUFFIX']  : env.StaticLibrary,
   env['SHLIBSUFFIX']: env.SharedLibrary,
   env['OBJSUFFIX']  : env.StaticObject,
   env['SHOBJSUFFIX']: env.SharedObject,
   env['TESTSUFFIX'] : functools.partial(
                           env.Program,
                           LIBS=['gtest', 'gtest_main', 'pthread'])
  }[ext](target, objects)
  test = None
  # If target is a unit test, execute it.
  if GetOption('test') and ext == env['TESTSUFFIX']:
    def run_test(target, source, env):
      for test in source:
        subprocess.check_call(test.get_path())
    test = env.Command(
               None,
               target,
               Action(run_test, 'Running unit test: $SOURCE'))
  env.Alias(target_path, [target, test])

